/*
#### Compute GC content and CpG deficiency from a _hardcoded_ file path

**Execute this file as `hyphy CG-01.md`**

Here we will use string operations (which is not efficient, but rather instructive).
We use HyPhy C-style comments to decorate the source, and hide Markdown in comments.

1. Read the alignment from a hardcoded file (relative to the current working directory) using the built-in `ReadDataFile` command.
2. Print the resulting `DataSet` object to the console using the default format using the `fprintf` command.

```
*/
      DataSet alignment = ReadDataFile ('ncov.fas');
      fprintf (stdout, alignment, "\n");
/*
```
The number of sequences in `alignment` are stored in the `alignment.species` variable.
1. We set up a C-style loop to iterate over the sequences, and dictionary variables (`nucleotides`, `dinucleotides`) to keep track of how many nucleotide characters and di-nucleotide patterns we found.
2. At each iteration we retrieve sequence strings using the built-in `GetDataInfo` procedure
3. For each sequence, we iterate over each character in the string (`Abs(string)` is the length of the string) and add a count to the dictionary
4. We count all the dinucleotides as well which is needed for the CpG calculation
5. Print the resulting counts to the console

Note that `dict[key] += 1` will work even if `key` is not in the dictionary, because in HyPhy `dict[key_not_in_dict]` returns `0`.

```
*/
nucleotides = {}; // empty dictionary
dinucleotides = {};

for (sequence_index = 0; sequence_index < alignment.species; sequence_index+=1) {
  GetDataInfo (ith_sequence, alignment, sequence_index);

  // handle the first nucleotide separately because it simplifies dinucleotide calculations below

  nucleotides [ith_sequence[0]] += 1;

  for (character_index = 1; character_index < Abs (ith_sequence); character_index += 1) {
    nucleotides [ith_sequence[character_index]] += 1;
    dinucleotides [ith_sequence[character_index-1][character_index]] += 1;
  }
}

fprintf (stdout, "\nNucleotide counts: ", nucleotides,
                 "\n\nDinucleotide counts: ", dinucleotides, "\n");

/*
```
Notice that in addition to the usual `A`, `C`, `G`, `T` nucleotides, there is also a count for `?` in the `nucleotides` dictionary

> Nucleotide counts: {
 "?":315,
 "A":5399,
 "C":3718,
 "G":3508,
 "T":6170
}

This is because HyPhy will automatically **pad** shorter sequences in the datafile with  `?`, as it **requires** aligned input. We handle it by only counting `A`,`C`,`G`,`T`.

1. Count all canonical nucleotides (this is denominator)
2. Compute frequencies of `C` and `G`; report `CG` content. Use `Format (number, width, precision)` to avoid reporting too many digits of precision for a percentage.
3. Count all canonical dinucleotides
4. Compute the frequency of `CG`, report `CpG` deficiency.

As reported in the literature, `CpG` are depleted (deficiency is <1) in SARS-CoV-2 genomes.
```
*/

total = nucleotides['A'] + nucleotides['C'] + nucleotides['G'] + nucleotides['T'];
fC = nucleotides['C']/total;
fG = nucleotides['G']/total;
fprintf (stdout, "\nAverage sequence CG content is ", Format ((fC+fG)*100, 8, 3), "%\n");

total_dinucs = dinucleotides['AA'] + dinucleotides['AC'] + dinucleotides['AG'] + dinucleotides['AT'] +dinucleotides['CA'] + dinucleotides['CC'] + dinucleotides['CG'] + dinucleotides['CT']
+dinucleotides['GA'] + dinucleotides['GC'] + dinucleotides['GG'] + dinucleotides['GT']
+dinucleotides['TA'] + dinucleotides['TC'] + dinucleotides['TG'] + dinucleotides['TT'];

fprintf (stdout, "\nCpG deficiency is ", Format ((dinucleotides['CG']/total_dinucs)/(fC*fG), 8, 3), "\n");

/*
```
*/
